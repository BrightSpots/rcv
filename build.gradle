plugins {
    id "application"
    id "checkstyle"
    id "idea"
    id "java"
    id "org.openjfx.javafxplugin" version "0.0.8"
    id "org.beryx.jlink" version "2.17.2"
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.apache.commons:commons-csv:1.8"
    compile "org.apache.poi:poi-ooxml:4.1.2"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.10.4"
    compile "com.fasterxml.jackson.core:jackson-core:2.10.2"
    compile "com.fasterxml.jackson.core:jackson-annotations:2.10.2"
    compile "com.fasterxml.jackson.core:jackson-databind:2.10.2"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.5.0-M1"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.5.0-M1"
}

// ### Application plugin settings
application {
    mainClassName = "$moduleName/network.brightspots.rcv.Main"
}

// Uncomment below to simulate running from the CLI
//run {
//    standardInput = System.in
//    args = ["-cli", "path/to/config"]
//}

// ### Checkstyle plugin settings
checkstyle {
    toolVersion = "8.30"
    // Keep the below file updated along with subsequent versions of Checkstyle (make sure to choose
    // the tag matching the toolVersion above):
    // https://github.com/checkstyle/checkstyle/blob/checkstyle-8.23/src/main/resources/google_checks.xml
    configFile = file("${configDir}/google_checks.xml")
}

// ### Idea plugin settings
idea {
    module {
        outputDir = file("out/production/classes")
    }
}

// ### Java plugin settings
sourceCompatibility = JavaVersion.VERSION_11

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

// ### JavaFX plugin settings
javafx {
    version = "13.0.2"
    modules = ["javafx.controls", "javafx.fxml"]
}

def JLINK_DIR = "$buildDir/rcv"

// ### jlink plugin settings
jlink {
    imageDir = file(JLINK_DIR)
    imageZip = file(JLINK_DIR + ".zip")
    addOptions '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages'
    mergedModule {
        requires "java.xml"
    }
    launcher {
        name = "rcv"
    }
}

def docsToCopy = copySpec {
    from("$projectDir") {
        include "README.md", "LICENSE"
    }
}

def sampleInputToCopy = copySpec {
    includeEmptyDirs = false
    from("$projectDir/src/test/resources/network/brightspots/rcv/test_data") {
        include "2015_portland_mayor/2015_portland_mayor_config.json"
        include "2015_portland_mayor/2015_portland_mayor_cvr.xlsx"
        include "sample_interactive_tiebreak/sample_interactive_tiebreak_config.json"
        include "sample_interactive_tiebreak/sample_interactive_tiebreak_sequential_config.json"
        include "sample_interactive_tiebreak/sample_interactive_tiebreak_cvr.xlsx"
        include "precinct_example/precinct_example_config.json"
        include "precinct_example/precinct_example_cvr.xlsx"
        include "dominion_cvr_conversion_alaska/*.json"
        exclude "output"
        exclude "**/*expected*"
    }
}

tasks.jlink.doLast {
    copy {
        with docsToCopy
        into JLINK_DIR + "/docs"
    }
    copy {
        with sampleInputToCopy
        into JLINK_DIR + "/sample_input"
    }
}
